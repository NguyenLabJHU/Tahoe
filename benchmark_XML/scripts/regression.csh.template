#!/bin/csh
# $Id: regression.csh.template,v 1.4 2001-09-23 18:39:43 paklein Exp $

# username on Tahoe server
set TAHOE_USER =

# directory containing working copies of the tahoe modules
set W_DIR =

# host name
set HOST =

# SEACAS
setenv ACCESS 

######### hopefully no changes will be required below #########

# set path
set path=(/bin /usr/bin /usr/local/bin /usr/ccs/bin /usr/sbin /usr/local/ssh /usr/ucb /etc /project/local/bin $path)

# mail command
set MAILCMD = /usr/bin/mail

# CVS
setenv CVSROOT ${TAHOE_USER}@tahoe.ca.sandia.gov:/usr/local/cvsrep
setenv CVS_RSH ssh

# remove toolbox library
if (-e ${W_DIR}/toolbox/lib/libtoolbox.a) then
	/bin/rm ${W_DIR}/toolbox/lib/libtoolbox.a
endif
#        remove tahoe
if (-e ${W_DIR}/tahoe/tahoe) then
	/bin/rm ${W_DIR}/tahoe/tahoe
endif
#        update macros
echo "updating MACROS"
cd ${W_DIR}/macros; cvs -q update -Pd
#        update and build toolbox
echo "checking out and building TOOLBOX"
cd ${W_DIR}/toolbox; cvs -q update -Pd ; make -s init ; \
 (make -s build >!  ../make.stdout) >&! ../make.stderr
#        update and build tahoe
if (-e ${W_DIR}/toolbox/lib/libtoolbox.a) then
	echo "checking out and building TAHOE"
	cd ${W_DIR}/tahoe;   cvs -q update -Pd ; make -s init ; \
 	(make -s build >>! ../make.stdout) >>&! ../make.stderr
endif
#        update and build comparator
if (-e ${W_DIR}/toolbox/lib/libtoolbox.a) then
	echo "checking out and building COMPARATOR"
	cd ${W_DIR}/benchmark/comparator;   cvs -q update -Pd ; make -s init ; \
 	(make -s build >>! ../make.stdout) >>&! ../make.stderr
endif
#        update and run regression tests
if (-e ${W_DIR}/tahoe/tahoe && -e ${W_DIR}/benchmark/comparator/compare) then
    echo  "running BENCHMARKs"
    cd ${W_DIR}/benchmark; \
	cvs -q update -Pd ; \
	make -s clean; \
	${W_DIR}/tahoe/tahoe -f ${W_DIR}/benchmark/run.batch >&! test.stdout
    echo  "running COMPARATOR"
    cd ${W_DIR}/benchmark; \
    rm compare.summary compare.console; \
    ${W_DIR}/benchmark/comparator/compare -f run.batch >&! compare.console
    if(! -e ${W_DIR}/benchmark/compare.summary) then
#       test/s failed - did not complete
	echo "BENCHMARKs failed"
		/usr/bin/echo "Subject: TAHOE: regression FAIL ${HOST}\nTAHOE regression FAIL:" `date` > ${W_DIR}/benchmark/tmp
		/bin/cat ${W_DIR}/benchmark/tmp ${W_DIR}/benchmark/compare.console > ${W_DIR}/benchmark/tmp_mail
    else if(`grep -c "FAIL" ${W_DIR}/benchmark/compare.summary` ) then
#       test/s failed
	echo "BENCHMARKs failed"
		/usr/bin/echo "Subject: TAHOE: regression FAIL ${HOST}\nTAHOE regression FAIL:" `date` > ${W_DIR}/benchmark/tmp
		/bin/cat ${W_DIR}/benchmark/tmp ${W_DIR}/benchmark/compare.summary > ${W_DIR}/benchmark/tmp_mail
    else
	echo "BENCHMARKs passed"
		/usr/bin/echo "Subject: TAHOE: regression PASS ${HOST}\nTAHOE regression PASS:" `date` > ${W_DIR}/benchmark/tmp
		/bin/cat ${W_DIR}/benchmark/tmp ${W_DIR}/benchmark/compare.summary > ${W_DIR}/benchmark/tmp_mail
	endif
else
#       make failed
		/usr/bin/echo "Subject: TAHOE: build FAIL ${HOST}\nTAHOE build FAIL:" `date` > ${W_DIR}/benchmark/tmp
		/bin/cat ${W_DIR}/benchmark/tmp ${W_DIR}/make.stderr > ${W_DIR}/benchmark/tmp_mail
endif

# send it
scp ${W_DIR}/benchmark/tmp_mail ${TAHOE_USER}@tahoe.ca.sandia.gov:/home/paklein/admin/distribute/mail.${HOST}

