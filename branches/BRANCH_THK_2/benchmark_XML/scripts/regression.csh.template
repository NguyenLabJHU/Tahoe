#!/bin/csh
# $Id: regression.csh.template,v 1.14 2003-04-07 19:02:56 paklein Exp $

# username on Tahoe server
set TAHOE_USER =

# directory containing working copies of the tahoe modules
set W_DIR =

# host name - used only for subject line of test report
set HOST =

# need an echo
set ECHO = 'echo'

# SEACAS - Sandia-only, ignore otherwise  
setenv ACCESS

# installed modules
set MODULES = (macros toolbox tahoe benchmark)
#set MODULES= (macros toolbox tahoe benchmark development)

######### hopefully no changes will be required below #########

# set path
set path=(${W_DIR}/benchmark/scripts ${W_DIR}/bin /bin /usr/bin /usr/local/bin /usr/X11R6/bin /usr/bin/X11 /usr/ccs/bin /usr/sbin /usr/local/ssh /usr/ucb /etc /usr/openwin/bin /project/local/bin $path)

# CVS
setenv CVS_RSH ssh

# name of the Tahoe executable
set TAHOE = tahoe

# build modules (in build order)
set BUILD_MODULES = (toolbox tahoe)

# command
set RM = 'rm -f'
set MV = 'mv -f'

###################### regression test ########################
# clear log files
cd ${W_DIR}
if (-e make.stderr) ${RM} make.stderr
if (-e make.stdout) ${RM} make.stdout
#
# resolve test requestor - first command line argument of $TAHOE_USER
#
if ($#argv > 0) then
	set REQUESTOR = $1
else
	set REQUESTOR = $TAHOE_USER
endif
echo "running regression requested by: ${REQUESTOR}"
#
# get latest files from the CVS repository
#
${ECHO} "updating source from CVS repository..."
foreach mod ($MODULES)
	${ECHO} "updating macros $mod..."
	cd ${W_DIR}/$mod
	(cvs -q update -Pd >>! ${W_DIR}/make.stdout) >>&! ${W_DIR}/make.stderr
end
#
# remove libraries and executables
#
${ECHO} "removing object code..."
foreach mod ($BUILD_MODULES)
	cd ${W_DIR}/$mod
	if (-e ./lib/lib$mod.a) then
		${RM} ./lib/lib$mod.a
	endif
end
if (-e ${W_DIR}/tahoe/${TAHOE}) then
	${RM} ${W_DIR}/tahoe/${TAHOE}
endif
#
# rebuild libraries and executables
#
${ECHO} "rebuilding object code..."
foreach mod ($BUILD_MODULES)
	cd ${W_DIR}/$mod
	(make -s init build >>! ${W_DIR}/make.stdout) >>&! ${W_DIR}/make.stderr
end
#
# update comparator
#
${ECHO} "rebuilding comparator..."
cd ${W_DIR}/benchmark/comparator
(make -s init build >>! ${W_DIR}/make.stdout) >>&! ${W_DIR}/make.stderr
#
# run tests
#
if (-x ${W_DIR}/tahoe/${TAHOE} && -x ${W_DIR}/benchmark/comparator/compare) then

    cd ${W_DIR}/benchmark
	
	# run benchmarks
    ${ECHO}  "running BENCHMARKs..."
	make -s clean
	${W_DIR}/tahoe/tahoe -f ${W_DIR}/benchmark/run.batch >&! test.stdout

	# test results 
    ${ECHO}  "running COMPARATOR..."
	if (-e compare.summary) ${RM} compare.summary
	if (-e compare.console) ${RM} compare.console
    ${W_DIR}/benchmark/comparator/compare -f run.batch >&! compare.console

	# assess results
    if (! -e ${W_DIR}/benchmark/compare.summary) then
    	# benchmarks did not run
		${ECHO} "BENCHMARKs failed"
		${ECHO} "Subject: TAHOE: regression FAIL ${HOST}" > ${W_DIR}/benchmark/tmp
		${ECHO} "TAHOE regression FAIL:" `date` >> ${W_DIR}/benchmark/tmp
		${ECHO} "test requested by: ${REQUESTOR}" >> ${W_DIR}/benchmark/tmp
		cat ${W_DIR}/benchmark/tmp ${W_DIR}/benchmark/compare.console > ${W_DIR}/benchmark/tmp_mail
	else if (`grep -c "FAIL" ${W_DIR}/benchmark/compare.summary` ) then
		# test/s failed
		${ECHO} "BENCHMARKs failed"
		${ECHO} "Subject: TAHOE: regression FAIL ${HOST}" > ${W_DIR}/benchmark/tmp
		${ECHO} "TAHOE regression FAIL:" `date` >> ${W_DIR}/benchmark/tmp
		${ECHO} "test requested by: ${REQUESTOR}" >> ${W_DIR}/benchmark/tmp		
		cat ${W_DIR}/benchmark/tmp ${W_DIR}/benchmark/compare.summary > ${W_DIR}/benchmark/tmp_mail
    else
    	# tests passed
		${ECHO} "BENCHMARKs passed"
		${ECHO} "Subject: TAHOE: regression PASS ${HOST}" > ${W_DIR}/benchmark/tmp
		${ECHO} "TAHOE regression PASS:" `date` >> ${W_DIR}/benchmark/tmp
		${ECHO} "test requested by: ${REQUESTOR}" >> ${W_DIR}/benchmark/tmp
		cat ${W_DIR}/benchmark/tmp ${W_DIR}/benchmark/compare.summary > ${W_DIR}/benchmark/tmp_mail
	endif
	
# build failed
else
	${ECHO} "Subject: TAHOE: build FAIL ${HOST}" > ${W_DIR}/benchmark/tmp
	${ECHO} "TAHOE build FAIL:" `date` >> ${W_DIR}/benchmark/tmp
	${ECHO} "test requested by: ${REQUESTOR}" >> ${W_DIR}/benchmark/tmp
	cat ${W_DIR}/benchmark/tmp ${W_DIR}/make.stderr > ${W_DIR}/benchmark/tmp_mail
endif
#
# send it
#
scp ${W_DIR}/benchmark/tmp_mail ${TAHOE_USER}@tahoe.ca.sandia.gov:/incoming/benchmark/mail.${HOST}
#
# run additional tests
#
# cd ${W_DIR}/benchmark/scripts; csh -f parallel.csh ${REQUESTOR} >& parallel.LOG
