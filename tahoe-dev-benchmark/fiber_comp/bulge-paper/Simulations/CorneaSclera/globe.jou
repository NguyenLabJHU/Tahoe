### DIMENSIONS [mm]
### cornea radii
# {Rnt = 17.9829}
# {Ris = 17.7144}
# {Ris = Rnt}
# {Rz = 24.193}
### globe radii
# {Rg = 28.0}
### meridians
# {Mnt = 12.75}
# {Mis = 10.25}
### apex thickness
# {apex_thk = 1.15}
# NOTE : grade 0.5 --> 0.65 (0.8 limbus humans)
# {thk_factor = 1.55}
# {thk_factor = 1.0}
# {nt_thk = thk_factor*apex_thk*Rnt/Rz}
# {is_thk = thk_factor*apex_thk*Ris/Rz}
### sclera thickness
# {sclera_thk = 1.5}
### distance between cornea & globe centers (from Mnt)
# {Zn = Rz * sqrt(1 - (Mnt/Rnt)**2) - Rg * sqrt(1 - (Mnt/Rg)**2) }
# {mesh_size = 1.0}
### elements through thickness (interval)
# {thk_int = 2}
#
graphics mode wireframe
################################################################
# solid model 
################################################################
# cornea
create sphere radius 1
volume 1 scale x {Rnt} y {Ris} z {Rz}
create sphere radius 1
volume 2 scale x {Rnt-nt_thk} y {Ris-is_thk} z {Rz-apex_thk}
section volume 1 2 with zplane offset {Rz-Rg}
# globe
create sphere radius 1
volume 3 scale x {Rg} y {Rg} z {Rg}
volume 3 copy scale {(Rg-sclera_thk)/Rg} 
move volume 1 2 z {-Zn}
#pause
# create inner and outer
unite volume 1 3
unite volume 2 4
subtract volume 2 from volume 1
#pause
section volume all with xplane
section volume all with yplane
section volume all with zplane
create curve vertex 18 15
webcut volume 5 sweep curve {Id("curve")} zaxis angle 90
#webcut volume all with cylinder radius {Mnt} axis z
move volume all z {Zn - Rz}
zoom reset
# output pic
#graph mode smoothshade
rotate -90 about x
#pause
#hardcopy "model.pnm" pnm
#hardcopy "model.jpg" jpg

###############################################################
# mesh
###############################################################
imprint all
merge all
volume all interval size {mesh_size}
curve 31 41 21 40 33 interval 3
# cornea 
surface 24 scheme triprimitive
volume 6 scheme sweep source surface 24 target surface 22
mesh volume 6
#pause
# globe
volume 5 scheme sweep source surface 32 target surface 33
mesh volume 5
# labeling
block 1 volume 6
block 2 volume 5
nodeset 2 volume 5
# x,y,z bcs
nodeset 10 surface with x_coord = 0
nodeset 20 surface with y_coord = 0
nodeset 30 surface with z_coord = {Zn - Rz}
# pressure bc
sideset 40 surface 22 28
sideset 41 surface 22
sideset 42 surface 28
# outer surface of cornea
nodeset 100 surface 24
sideset 100 surface 24
nodeset 101 curve 26
nodeset 102 curve 18
# inner surface 
nodeset 110 surface 22
# output
export genesis "globe.exo" overwrite
exit

